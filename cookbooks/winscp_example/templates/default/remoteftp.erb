@echo off
set SESSION=sftp://<%= @ftpuser %>:<%= @ftppwd %>@<%= @ftpserver %>/ -hostkey=*
set REMOTE_PATH=<%= @ftppath %>
set LOCAL_PATH=<%= @sftp_home %>
 
for /F %%i in ('dir /b "%LOCAL_PATH%\outgoing\*.*"') do (
   REM echo Folder is NON empty
   goto upload
)
goto end

:upload
echo open %SESSION% >> script.tmp
echo lcd %LOCAL_PATH%\outgoing >> script.tmp
echo cd %REMOTE_PATH% >> script.tmp
echo put * >> script.tmp
echo exit >> script.tmp
"C:\WinSCP\winscp.com" /script=script.tmp 
set RESULT=%ERRORLEVEL% 
del script.tmp

if %RESULT% neq 0 goto error 
echo Success! All files uploaded Successfully 

set datef=%date:~-4%%date:~4,2%%date:~7,2%
set timef=%time:~0,2%%time:~3,2%%time:~6,2%
set datetimef=%datef%-%timef%
md "%LOCAL_PATH%\archive\%datetimef%"
move "%LOCAL_PATH%\outgoing\*" "%LOCAL_PATH%\archive\%datetimef%" 
exit /b %ERRORLEVEL%

:error 
echo Error! One or More files failed to upload 
exit /b %RESULT%

:end
echo No action to take!
exit /b 0

